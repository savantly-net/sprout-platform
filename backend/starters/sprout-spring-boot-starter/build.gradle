plugins {
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'
	id 'java'
	id 'java-library'
	id 'signing'
	id 'maven'
 	id 'jacoco'
 	id 'org.sonarqube'
	id "org.owasp.dependencycheck" version "6.2.2"
	id 'org.asciidoctor.convert' version '1.5.6'
}

archivesBaseName = name
description = "A Spring Boot starter for Sprout Projects"
sourceCompatibility = '1.8'

ext {
	sonarProjectKey = "savantly-net_sprout-platform_starter"
}

apply from: "${rootProject.projectDir}/gradle/sonar.gradle"
apply from: "${rootProject.projectDir}/gradle/sonatype.gradle"

sourceSets {
    dev {
        java {
            compileClasspath += main.compileClasspath
            runtimeClasspath += main.runtimeClasspath
            srcDirs += ['src/dev/java', 'src/dev/resources']
        }
    }
}

// run `./gradlew :sprout-spring-boot-starter:bootRun -Psuspend=y` to suspend for debugger connection
def suspend = project.ext.properties.suspend
if(!suspend) {
	suspend = 'n'
}
bootRun {
	main = 'example.DevApplication'
    classpath = sourceSets.dev.runtimeClasspath
	sourceResources sourceSets.main
    jvmArgs=[
  	  "-agentlib:jdwp=transport=dt_socket,server=y,suspend=$suspend,address=18080"
    ]
}

bootJar {
    enabled = false
}

jar {
  onlyIf { true }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
	//api platform('software.amazon.awssdk:bom:2.14.3')
	api project(':sprout-core')
	
	api 'org.freemarker:freemarker',
		'org.springframework.boot:spring-boot-starter-web',
		'org.springframework.boot:spring-boot-starter-security',
		'org.springframework.boot:spring-boot-starter-oauth2-resource-server',
		'org.springframework.boot:spring-boot-starter-oauth2-client',
		'org.springframework.boot:spring-boot-starter-data-rest',
		'org.springframework.boot:spring-boot-starter-actuator',
		'org.springframework.boot:spring-boot-starter-activemq',
		
		'org.springframework.security:spring-security-oauth2-client',
		
		'org.springframework:spring-context-support',
		
		'net.savantly.security:spring-role-permissions:0.0.2.RELEASE',
		
		'io.reactivex:rxjava',
		"org.zalando:problem-spring-web:${problem_version}",
		"io.jsonwebtoken:jjwt-api:${json_web_token_version}",
		"io.jsonwebtoken:jjwt-impl:${json_web_token_version}",
		"io.jsonwebtoken:jjwt-jackson:${json_web_token_version}",
		
		"org.springdoc:springdoc-openapi-ui:${springdoc_version}",
		"org.springdoc:springdoc-openapi-data-rest:${springdoc_version}",
		"org.springdoc:springdoc-openapi-security:${springdoc_version}",
		
		"com.fasterxml.jackson.datatype:jackson-datatype-hibernate5",
		"com.fasterxml.jackson.module:jackson-module-afterburner",
		'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.1',
		'org.apache.httpcomponents:httpclient:4.5.13',
		'software.amazon.awssdk:s3:2.14.3',
		'com.opentable.components:otj-pg-embedded:0.13.1',
		'org.flywaydb:flyway-core'
			
	compileOnly 'org.projectlombok:lombok',
		'javax.interceptor:javax.interceptor-api:1.2'
    annotationProcessor 'org.projectlombok:lombok',
        'org.springframework.boot:spring-boot-configuration-processor'
    
	developmentOnly "org.springframework.boot:spring-boot-devtools"
	
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test',
		'com.h2database:h2',// need to create sql init scripts for H2
		'org.springframework.restdocs:spring-restdocs-mockmvc',
		'org.postgresql:postgresql'
	
	testCompile "org.testcontainers:testcontainers:1.15.3",
		"org.testcontainers:junit-jupiter:1.15.3",
		"org.testcontainers:postgresql:1.15.3"
}

test {
	useJUnitPlatform()
}

dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}