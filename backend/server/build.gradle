plugins {
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'
	id 'java'
 	id 'jacoco'
 	id 'org.sonarqube'
	id "org.owasp.dependencycheck" version "5.3.1"
}

archivesBaseName = name
description = "A preconfigured Sprout Server Application"
sourceCompatibility = '1.8'

ext {
	sonarProjectKey = "savantly-net_sprout-platform_server"
}

apply from: "${rootProject.projectDir}/gradle/sonar.gradle"

// run `./gradlew bootRun -Psuspend=y` to suspend for debugger connection
def suspend = project.ext.properties.suspend
if(!suspend) {
	suspend = 'n'
}
bootRun {
  jvmArgs=[
  	"-agentlib:jdwp=transport=dt_socket,server=y,suspend=$suspend,address=18080"
  ]
}

bootJar {
    layered()
	exclude("**/application-local.yml")
}
// run `./gradlew :sprout-modules-forms:dev -Psuspend=y` to suspend for debugger connection
task dev(type: JavaExec) {
    group = "Execution"
    description = "Run the app class with JavaExecTask"
    //classpath = sourceSets.test.runtimeClasspath
    classpath += sourceSets.main.runtimeClasspath
    main = 'net.savantly.sprout.server.SproutApplication'
    jvmArgs=[
  	  "-agentlib:jdwp=transport=dt_socket,server=y,suspend=$suspend,address=18080"
    ]
}

task uploadArchives {
 // Don't publish the example server
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
	implementation project(':sprout-spring-boot-starter')
	//implementation 'com.h2database:h2'
	
	//runtimeOnly project(':sprout-admin')
	//runtimeOnly project(':web:sprout-web-ui')
	//runtimeOnly project(':sprout-modules-content')
	//runtimeOnly project(':sprout-modules-openTok')
	//runtimeOnly project(':sprout-modules-newsfeed')
	//runtimeOnly project(':sprout-modules-forms')
	//runtimeOnly project(':sprout-modules-example-panel')
	
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	
	runtimeOnly "io.jsonwebtoken:jjwt-impl:${json_web_token_version}",
		"io.jsonwebtoken:jjwt-jackson:${json_web_token_version}",
		'org.postgresql:postgresql'

	compileOnly 'org.projectlombok:lombok'
	
	developmentOnly "org.springframework.boot:spring-boot-devtools"
    annotationProcessor 'org.projectlombok:lombok',
        'org.springframework.boot:spring-boot-configuration-processor'
        
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}

