plugins {
	id 'org.springframework.boot' apply false
	id 'io.spring.dependency-management'
	id 'java'
	id 'java-library'
	id 'signing'
	id 'maven'
 	id 'jacoco'
 	id 'org.sonarqube'
	id "org.owasp.dependencycheck" version "5.3.1"
	id "com.github.node-gradle.node" version "2.2.0"
}

archivesBaseName = name
description = "Provides dynamic content management for the Sprout Platform"
sourceCompatibility = '1.8'

apply from: '../../gradle/sonar.gradle'
apply from: '../../gradle/sonatype.gradle'

repositories {
	mavenCentral()
}

node {
    /* gradle-node-plugin configuration
       https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md
       Task name pattern:
       ./gradlew npm_<command> Executes an NPM command.
    */
    // Version of node to use.
    version = '10.20.1'
    // Version of npm to use.
    //npmVersion = '6.9.0'
    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true
}

npm_run_build {
    inputs.files fileTree("src/main/browser")
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    outputs.dir "build/resources/main/public/content"
}

jar.dependsOn npm_run_build
npm_run_build.dependsOn npm_install

dependencies {
	api project(':sprout-core')
	api 'org.freemarker:freemarker'

	compileOnly 'org.projectlombok:lombok',
		'javax.interceptor:javax.interceptor-api:1.2',
		'javax.validation:validation-api:2.0.1.Final',
		"org.springframework.boot:spring-boot-devtools"
    annotationProcessor 'org.projectlombok:lombok',
        'org.springframework.boot:spring-boot-configuration-processor'
    
    testImplementation project(':sprout-module-test')
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test',
		'com.h2database:h2'
}

test {
	useJUnitPlatform()
}

dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}

