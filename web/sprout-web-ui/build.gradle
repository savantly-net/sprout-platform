buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}
apply plugin: 'base'
apply plugin: 'com.moowork.node' // gradle-node-plugin


node {
    /* gradle-node-plugin configuration
       https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md
       Task name pattern:
       ./gradlew npm_<command> Executes an NPM command.
    */
    // Version of node to use.
    version = '10.20.1'
    // Version of npm to use.
    //npmVersion = '6.9.0'
    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true
}

npm_run_build {
    inputs.files fileTree("src")
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    outputs.dir "${projectDir}/dist"
}
task copyDist(type: Copy) {
	dependsOn npm_run_build
	from 'dist'
    into "${projectDir}/build/classes/public"
}
task copyIndex(type: Copy) {
	dependsOn copyDist
	from 'dist/index.html'
	into "${projectDir}/build/classes/templates"
}
task packageNpmApp(type: Zip) {
    dependsOn copyIndex
    baseName 'sprout-admin'
    extension 'jar'
    destinationDir file("${projectDir}/target/")
    from("${projectDir}/build/classes/") {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        // into 'static' 
    }
}

configurations {
    npmResources
}
configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageNpmApp.archivePath) {
        builtBy packageNpmApp
        type "jar"
    }
}

assemble.dependsOn packageNpmApp

clean {
    delete packageNpmApp.archivePath
    delete 'dist'
    delete 'target'
}